plugins {
    id 'java'
    // Google Java Format
    id 'com.diffplug.spotless' version '6.23.3'
}

group = 'com.github.miyohide'
version = '1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation platform('software.amazon.awssdk:bom:2.25.1')
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.3'
    implementation 'com.amazonaws:aws-lambda-java-events:3.11.4'
    implementation 'software.amazon.awssdk:s3'
    implementation 'org.apache.logging.log4j:log4j-api:2.17.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.17.1'
    runtimeOnly 'org.apache.logging.log4j:log4j-slf4j18-impl:2.17.1'
    runtimeOnly 'com.amazonaws:aws-lambda-java-log4j2:1.6.0'
    runtimeOnly 'org.postgresql:postgresql:42.7.3'
}

// ビルドした結果と依存ラインタイムをまとめてzip化する
tasks.register('packageFat', Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
    archiveAppendix = 'fat'
}

// 依存ライブラリだけのパッケージを作る
tasks.register('packageLibs', Zip) {
    into('java/lib') {
        from configurations.runtimeClasspath
    }
    archiveAppendix = 'libs'
}

// 自分で実装したプログラムだけのパッケージを作る
tasks.register('packageSkinny', Zip) {
    from compileJava
    from processResources
    archiveAppendix = 'thin'
}

// buildタスクはpackageSkinnyタスクに依存する
build.dependsOn packageSkinny

spotless {
    java {
        googleJavaFormat()
    }
}
