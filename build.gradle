plugins {
    id 'java'
}

group = 'com.github.miyohide'
version = '1.0'

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'com.amazonaws:aws-lambda-java-core:1.2.3'
    implementation 'com.amazonaws:aws-lambda-java-events:3.11.3'
    runtimeOnly 'com.amazonaws:aws-lambda-java-log4j2:1.5.1'
    testImplementation platform('org.junit:junit-bom:5.9.1')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

// ビルドした結果と依存ラインタイムをまとめてzip化する
tasks.register('packageFat', Zip) {
    from compileJava
    from processResources
    into('lib') {
        from configurations.runtimeClasspath
    }
    archiveAppendix = 'fat'
}

// 依存ライブラリだけのパッケージを作る
tasks.register('packageLibs', Zip) {
    into('java/lib') {
        from configurations.runtimeClasspath
    }
    archiveAppendix = 'libs'
}

// 自分で実装したプログラムだけのパッケージを作る
tasks.register('packageSkinny', Zip) {
    from compileJava
    from processResources
    archiveAppendix = 'thin'
}

// buildタスクはpackageSkinnyタスクに依存する
build.dependsOn packageSkinny
